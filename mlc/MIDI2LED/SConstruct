#!python

includes = ['.', 'Common', 'Model']

MCU_FLAG = '-mmcu=atmega644p'

avr_flags = [
    MCU_FLAG,
    '-std=gnu99',
    '-fdata-sections',
    '-ffunction-sections',
    '-fpack-struct',
    '-fshort-enums',
    '-fno-exceptions',
    '-Wall',
]
avr_link_flags = [
    MCU_FLAG,
    '-Wl,--gc-sections',
    '-Wl,-Map=$TARGET\.map',
]
avr_flags_debug = [
    '-Og',
    '-g3',
    '-DDEBUG'
]
avr_flags_release = [
    '-Os',
]

env = Environment()
env['TOOLS'] = ['avr-gcc', 'avr-g++']
env['CC'] = 'avr-gcc'
env['CXX'] = 'avr-g++'
env['CPPPATH'] = includes
env['CFLAGS'] = avr_flags
env['CXXFLAGS'] = avr_flags
env['LINKFLAGS'] = avr_link_flags
env['LIBS'] = ['m']
env['PROGSUFFIX'] = '.elf'

if ARGUMENTS.get('VERBOSE') != '1':
    env['CCCOMSTR'] = 'Compiling $TARGET'
    env['LINKCOMSTR'] = 'Linking $TARGET'

env_debug = env.Clone()
env_debug.Append(CFLAGS=avr_flags_debug, CXXFLAGS=avr_flags_debug)
env_release = env.Clone()
env_release.Append(CFLAGS=avr_flags_release, CXXFLAGS=avr_flags_release)

Default('release')

SConscript('MIDI2LED.scons', variant_dir='debug', exports={'env': env_debug})
SConscript('MIDI2LED.scons', variant_dir='release', exports={'env': env_release})

# env_test = Environment()
